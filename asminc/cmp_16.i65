; assist_16 - cmp_16: 16 bit utility macros for the 65C02 processor.

; cmp_16 - Compare a 16 bit variable in memory with a value.
; Parameters:
;   var - a 16 bit variable.
;   value - an integer value to compare var with.
; Returns:
;   The N, V, Z, and C flags are set.
;   C is set if var >= value using unsigned comparison.
;   (N xor V) is cleared if var >= value using signed comparison.
;   Z is set if var = value
; Notes:
;   zp, abs : Clobbers the A register.
;   zpi     : Clobbers the A and Y registers.
;   zpy     : Clobbers the A register.
;   zx, abx : Clobbers the A register.
;   zy, aby : Clobbers the A register.
;
;   Optimized for special cases like values of $xx00.

.ifndef assist_cmp_16_included
assist_cmp_16_included = 1

.macro _cmp_var_16 var, value
  .local not_equal, all_done

  sec
  lda var                              ; Get the low byte of data.

  .if (!.const(value))||.lobyte(value) ; Can we skip the subtract?
    sbc #.lobyte(value)                ; Nope, gotta do the work!
  .endif

  bne not_equal                        ; Branch if low bytes not equal.

  lda var+1                            ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bra all_done

not_equal:                             ; Got here because low bytes not equal.
  lda var+1                            ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bne all_done                         ; If not zero we must skip over.
  inc                                  ; In this branch we must clear Z.

all_done:
.endmacro

.macro _cmp_vax_16 var, value
  .local not_equal, all_done

  sec
  lda var,x                            ; Get the low byte of data.

  .if (!.const(value))||.lobyte(value) ; Can we skip the subtract?
    sbc #.lobyte(value)                ; Nope, gotta do the work!
  .endif

  bne not_equal                        ; Branch if low bytes not equal.

  lda var+1,x                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bra all_done

not_equal:                             ; Got here because low bytes not equal.
  lda var+1,x                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bne all_done                         ; If not zero we must skip over.
  inc                                  ; In this branch we must clear Z.

all_done:
.endmacro

.macro _cmp_vay_16 var, value
  .local not_equal, all_done

  sec
  lda var,y                            ; Get the low byte of data.

  .if (!.const(value))||.lobyte(value) ; Can we skip the subtract?
    sbc #.lobyte(value)                ; Nope, gotta do the work!
  .endif

  bne not_equal                        ; Branch if low bytes not equal.

  lda var+1,y                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bra all_done

not_equal:                             ; Got here because low bytes not equal.
  lda var+1,y                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bne all_done                         ; If not zero we must skip over.
  inc                                  ; In this branch we must clear Z.

all_done:
.endmacro

.macro _cmp_zpp_16 var, value
  .local not_equal, all_done

  sec
  ldy #1
  lda (var)                            ; Get the low byte of data.

  .if (!.const(value))||.lobyte(value) ; Can we skip the subtract?
    sbc #.lobyte(value)                ; Nope, gotta do the work!
  .endif

  bne not_equal                        ; Branch if low bytes not equal.

  lda (var),y                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bra all_done

not_equal:                             ; Got here because low bytes not equal.
  lda (var),y                          ; Get the high byte of data.
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bne all_done                         ; If not zero we must skip over.
  inc                                  ; In this branch we must clear Z.

all_done:
.endmacro

.macro _cmp_zpy_16 var, value
  .local not_equal, all_done

  sec
  lda (var),y                          ; Get the low byte of data.

  .if (!.const(value))||.lobyte(value) ; Can we skip the subtract?
    sbc #.lobyte(value)                ; Nope, gotta do the work!
  .endif

  bne not_equal                        ; Branch if low bytes not equal.

  iny
  lda (var),y                          ; Get the high byte of data.
  dey
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bra all_done

not_equal:                             ; Got here because low bytes not equal.
  iny
  lda (var),y                          ; Get the high byte of data.
  dey
  sbc #.hibyte(value)                  ; Subtract the high bytes.
  bne all_done                         ; If not zero we must skip over.
  inc                                  ; In this branch we must clear Z.

all_done:
.endmacro

.macro cmp_16 dst,value
  .if (.match({dst}, {(name)}))
    _cmp_zpp_16 .mid(1, 1, {dst}),value
  .elseif (.match({dst}, {(name),y}))
    _cmp_zpy_16 .mid(1, 1, {dst}),value
  .elseif (.match({dst}, {name,x}))
    _cmp_vax_16 .mid(0, 1, {dst}),value
  .elseif (.match({dst}, {name,y}))
    _cmp_vay_16 .mid(0, 1, {dst}),value
  .else
    _cmp_var_16 {dst},value
  .endif
.endmacro

.endif
