; Tests for the Sweet-16 ported to the Commander X 16

sw16_sim_support = 1  ; Enable SW16 support the success/failure indicators.

.include "test65.i65"
.include "../utilities/assist_16/set_16.i65"
.include "../utilities/assist_16/eql_16.i65"
.include "../vm/sweet_16.a65"
.include "../vm/sweet_16_test.i65"

.export _main
.pc02

.data
  ; Reserve space for the Sweet-16 stack.
  ; Note: The Sweet-16 stack grows upward. The opposite of usual.
  sw16_stack: .res 10

.code
_main:

  ; Set up some initial register values.
  begin_sw16
  set R12, sw16_stack
  set R0,$1234
  sw16_fail_eq 30
  sw16_fail_cs 31
  set R1,$5678
  sw16_fail_eq 32
  sw16_fail_cs 33
  end_sw16

  ; Test some register values.
  eql_16 sw16_r0,$1234
  fail_ne 10
  eql_16 sw16_r1,$5678
  fail_ne 11

  ; Add some numbers and test.
  begin_sw16
  add R1
  sw16_fail_cs 34
  end_sw16
  eql_16 sw16_r0,$68AC
  fail_ne 12
  eql_16 sw16_r1,$5678
  fail_ne 13

  ; Restore values by subtracting and test.
  begin_sw16
  sub R1
  sw16_fail_cc 35
  end_sw16
  eql_16 sw16_r0,$1234
  fail_ne 14
  eql_16 sw16_r1,$5678
  fail_ne 15

  ; Test using the Fibonacci sequence.
  begin_sw16
  set R0,$0
  set R1,$1
  set R5,fib_table  ; R5 points to the expected values. 0 is the end marker.

test_loop:
  bs fib            ; Compute the next value in the series.

  ; Get the test value into R4
  st  R2            ; Save R0
  ldd @R5           ; Get the next table entry.
  bz  fib_test_done ; However, zero marks the end of the test.
  st  R4
  ld  R2            ; Restore R0

  ; Do the values match?
  cpr R4
  sw16_fail_ne 20

  br  test_loop     ; Go again!

fib_test_done:
  end_sw16

  jmp test_group_2

  .setcpu "sweet16"
fib:
  ; Swap R0 and R1
  st R2
  ld R1
  st R3
  ld R2
  st R1
  ld R3

  ; Add R1 to R0
  add R1
  rs
  .pc02

  tests_fail 254

fib_table:
  .word 1
  .word 1
  .word 2
  .word 3
  .word 5
  .word 8
  .word 13
  .word 21
  .word 34
  .word 55
  .word 89
  .word 144
  .word 233
  .word 377
  .word 610
  .word 987
  .word 1597
  .word 2584
  .word 4181
  .word 6765
  .word 10946
  .word 17711
  .word 28657
  .word 46368
  .word 0

test_group_2:

   tests_pass
