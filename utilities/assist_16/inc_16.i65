; inc_16 - Increment a 16 bit variable in memory.
; Parameters:
;   var - a 16 bit variable.
; Notes:
;   zp, abs : Clobbers the Z and N flags.
;   zpi     : Clobbers the A and Y register, Z and N flags.
;   zpy     : Clobbers the A register and the Z and N flags.
;   zx, abx : Clobbers the Z and N flags.
;   zy, aby : Clobbers the Z and N flags.

.ifndef assist_inc_16_included
assist_inc_16_included = 1

.macro _inc_var_16 var
  .local no_wrap
  inc var                              ; Increment the low byte.
  bne no_wrap                          ; Skip to end if no roll-over.
  inc var+1                            ; Propagate to the high byte.
no_wrap:
.endmacro

.macro _inc_vax_16 var
  .local no_wrap
  inc var,x                            ; Increment the low byte.
  bne no_wrap                          ; Skip to end if no roll-over.
  inc var+1,x                          ; Propagate to the high byte.
no_wrap:
.endmacro

.macro _inc_vay_16 var
  .local no_wrap
  lda var,y                            ; Increment the low byte the hard way.
  inc
  sta var,y
  bne no_wrap                          ; Skip to end if no roll-over.
  lda var+1,y                          ; Increment the high byte the hard way.
  inc
  sta var+1,y
no_wrap:
.endmacro

.macro _inc_zpp_16 var
  .local no_wrap
  lda (var)                            ; Increment the low byte the hard way.
  inc
  sta (var)
  bne no_wrap                          ; Skip to end if no roll-over.
  ldy #1                               ; Use Y to access the high byte.
  lda (var),y                          ; Increment the high byte the hard way.
  inc
  sta (var),y
no_wrap:
.endmacro

.macro _inc_zpy_16 var
  .local no_wrap
  lda (var),y                          ; Increment the low byte the hard way.
  inc
  sta (var),y
  bne no_wrap                          ; Skip to end if no roll-over.
  iny                                  ; Step to the high byte.
  lda (var),y                          ; Increment the high byte the hard way.
  inc
  sta (var),y
  dey                                  ; Step back to the low byte.
no_wrap:
.endmacro

.macro inc_16 dst
  .if (.match({dst}, {(name)}))
    _inc_zpp_16 .mid(1, 1, {dst})
  .elseif (.match({dst}, {(name),y}))
    _inc_zpy_16 .mid(1, 1, {dst})
  .elseif (.match({dst}, {name,x}))
    _inc_vax_16 .mid(0, 1, {dst})
  .elseif (.match({dst}, {name,y}))
    _inc_vay_16 .mid(0, 1, {dst})
  .else
    _inc_var_16 {dst}
  .endif
.endmacro

.endif
