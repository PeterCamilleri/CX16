; Macros to increment 16-bit values.

.ifndef assist_dec_16_included
assist_dec_16_included = 1

.macro _dec_var_16 var
  .local no_wrap
  lda var                              ; Test the low byte.
  bne no_wrap                          ; Skip over if it won't roll-over.
  dec var+1                            ; Decrement the high byte.
no_wrap:
  dec var                              ; Decrement the low byte.
.endmacro

.macro _dec_vax_16 var
  .local no_wrap
  lda var,x                            ; Test the low byte.
  bne no_wrap                          ; Skip over if it won't roll-over.
  dec var+1,x                          ; Decrement the high byte.
no_wrap:
  dec var,x                            ; Decrement the low byte.
.endmacro

.macro _dec_vay_16 var
  .local no_wrap
  lda var,y                            ; Test the low byte.
  bne no_wrap                          ; Skip over if it won't roll-over.
  lda var+1,y                          ; Decrement the high byte the hard way.
  dec
  sta var+1,y
  lda var,y                            ; Decrement the low byte the hard way.
no_wrap:
  dec
  sta var,y
.endmacro

.macro _dec_zpp_16 var
  .local no_wrap
  lda (var)                            ; Test the low byte.
  bne no_wrap                          ; Skip over if it won't roll-over.
  ldy #1                               ; Use Y to access the high byte.
  lda (var),y                          ; Decrement the high byte the hard way.
  dec
  sta (var),y
  lda (var)                            ; Decrement the low byte the hard way.
no_wrap:
  dec
  sta (var)
.endmacro

.macro _dec_zpy_16 var
  .local no_wrap
  lda (var),y                          ; Test the low byte.
  bne no_wrap                          ; Skip over if it won't roll-over.
  iny                                  ; Step to the high byte.
  lda (var),y                          ; Decrement the high byte the hard way.
  dec
  sta (var),y
  dey                                  ; Step back to the low byte.
  lda (var),y                          ; Decrement the low byte the hard way.
no_wrap:
  dec
  sta (var),y
.endmacro

; dec_16
; Decrement a 16 bit variable in memory.
; Parameters:
;   var - a 16 bit variable.
; Notes:
;   zp, abs : Clobbers the A register, Z and N flags.
;   zpi     : Clobbers the A and Y register, Z and N flags.
;   zpy     : Clobbers the A register and the Z and N flags.
;   zx, abx : Clobbers the A register, Z and N flags.
;   zy, aby : Clobbers the A register, Z and N flags.
;
.macro dec_16 dst
  .if (.match({dst}, {(name)}))
    _dec_zpp_16 .mid(1, 1, {dst})
  .elseif (.match({dst}, {(name),y}))
    _dec_zpy_16 .mid(1, 1, {dst})
  .elseif (.match({dst}, {name,x}))
    _dec_vax_16 .mid(0, 1, {dst})
  .elseif (.match({dst}, {name,y}))
    _dec_vay_16 .mid(0, 1, {dst})
  .else
    _dec_var_16 {dst}
  .endif
.endmacro


.endif
