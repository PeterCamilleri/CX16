; Add signed comparison branches to the W65C02S.
; Warning: The cmp instruction does not set the V flag and should not be used
; to perform signed comparisons. For signed comparisons, use sbc instead.

.ifndef signed_branch_included
signed_branch_included = 1

; Branch if signed less than
.macro bslt target
   .local not_taken, sign_flipped
   bvs sign_flipped
   bmi target
   bra not_taken
sign_flipped:
  bpl target
not_taken:
.endmacro

; Branch if signed less than or equal to
.macro bsle target
   .local not_taken, sign_flipped
   beq target
   bvs sign_flipped
   bmi target
   bra not_taken
sign_flipped:
  bpl target
not_taken:
.endmacro

; Branch if signed greater than or equal to
.macro bsge target
   .local not_taken, sign_flipped
   bvs sign_flipped
   bpl target
   bra not_taken
sign_flipped:
  bmi target
not_taken:
.endmacro

; Branch if signed greater than
.macro bsgt target
   .local not_taken, sign_flipped
   beq not_taken
   bvs sign_flipped
   bpl target
   bra not_taken
sign_flipped:
   bmi target
not_taken:
.endmacro

.endif
