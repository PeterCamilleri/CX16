; Add signed comparison long branches to the 65C02

.ifndef long_signed_branch_included
long_signed_branch_included = 1

; Branch if signed less than
.macro lbslt target
   .local taken, not_taken, sign_flipped
   bvs sign_flipped
   bpl not_taken
taken:
   jmp target
sign_flipped:
   bpl taken
not_taken:
.endmacro

; Branch if signed less than or equal to
.macro lbsle target
   .local taken, not_taken, sign_flipped
   beq taken
   bvs sign_flipped
   bpl not_taken
taken:
   jmp target
sign_flipped:
  bpl taken
not_taken:
.endmacro

; Branch if signed greater than or equal to
.macro lbsge target
   .local taken, not_taken, sign_flipped
   bvs sign_flipped
   bmi not_taken
taken:
  jmp target
sign_flipped:
  bmi taken
not_taken:
.endmacro

; Branch if signed greater than
.macro lbsgt target
   .local taken, not_taken, sign_flipped
   beq not_taken
   bvs sign_flipped
   bmi not_taken
taken:
  jmp target
sign_flipped:
  bmi taken
not_taken:
.endmacro

.endif
